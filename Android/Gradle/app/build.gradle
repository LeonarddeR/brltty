plugins {
  id "com.android.application"
  id "com.github.triplet.play" version "3.12.0"
}

description = """
"${project.name}" implements the accessibility service for "${rootProject.name}" on Android.
"""

apply from: "brltty-config.gradle"
apply from: "brltty-assets.gradle"

dependencies {
  implementation project(":core")
  implementation "androidx.core:core:1.16.0"
}

android {
  namespace "org.a11y.brltty.android"

  compileSdkVersion "android-35"
  buildToolsVersion "35.0.0"

//ndkVersion "21.4.7075529" // r21e
  ndkVersion "27.1.12297006" // r27b
//ndkVersion "28.2.13676358" // r28c

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  lintOptions {
    warning "MissingTranslation"
    error "Untranslatable"
  }

  buildFeatures {
    buildConfig = true
  }

  defaultConfig {
    targetSdkVersion 34
    minSdkVersion 24

    versionName brltty.config.versionName
    versionCode brltty.config.versionCode as int

    ndk {
      splits {
        abi {
          enable true
          universalApk true
          reset()

          brltty.native.abiList.each { abi ->
            include abi
          }
        }
      }
    }
  }

  signingConfigs {
    release {
      def signingProperties = brltty.loadProperties("credentials/sign-gradle.properties")
      storeFile     signingProperties.storeFile
      storePassword signingProperties.storePassword
      keyAlias      signingProperties.keyAlias
      keyPassword   signingProperties.keyPassword
    }
  }

  buildTypes {
    debug {
      ndk {
        abiFilters "armeabi-v7a"
      }
    }

    release {
      signingConfig signingConfigs.release

      minifyEnabled false
      proguardFile getDefaultProguardFile("proguard-android.txt")

      ndk {
        debugSymbolLevel "FULL" // FULL, SYMBOL_TABLE
      }
    }
  }

  play {
    track = "production" // internal, alpha, beta, production
    defaultToAppBundles = true
    serviceAccountCredentials = rootProject.file("credentials/publish-play.json")
  }
}
